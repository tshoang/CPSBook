Theorem "CPSBook/Ex05.05/Valid"
Description "Ex5.5: (\forall t  t >= 0 -> [{x := y(t);}](q(x) -> p(x))) -> [{x' = f(x) & q(x)}]p(x) is sound".
/*
 * Copyright (c) 2024 University of Southampton.
 * Licensed under MIT license.
 * 
 * @author Son Hoang -- Initial model
 */

/*
 * Encoding for some generic predicates P, Q, E, and expression D, and program A.
 */
Definitions
  Bool p(Real x);
  Bool q(Real x);
  Real f(Real x);
  Real y(Real y);
  HP   P ::= {x'=f(x) & q(x)};
End.

ProgramVariables
  Real x;
End.

/*
 * We cannot encode the generic solution for differential equation, so we
 * implicitly apply the rule *solve* immediately. The original rules should be
 * \forall t  (t >= 0 -> [{x := y(t);}](q(x) -> p(x)) -> [{x' = f(x) & q(x)}]p(x))
 */
Problem
  (\forall t  (t >= 0 -> [{x := y(t);}](q(x) -> p(x)))) -> 
  
    (\forall t (t >= 0 -> (
        (\forall s (0 <= s & s <= t -> q(y(s))))
            -> [{x := y(t);}]p(x)
        )
    ))
End.

/*
 * Beside the logical rules (Chapter 6), the proof uses the rules for assignment
 * (*assignb*) and implicitly the differential equation solution (*solve*).
 */
Tactic "CPSBook/Ex05.05/Valid: Interactive"
implyR('R=="\forall t (t>=0->[x:=y(t);](q(x)->p(x)))->\forall t (t>=0->\forall s (0<=s&s<=t->q(y(s)))->[x:=y(t);]p(x))");
allR('R=="\forall t (t>=0->\forall s (0<=s&s<=t->q(y(s)))->[x:=y(t);]p(x))");
implyR('R=="t>=0->\forall s (0<=s&s<=t->q(y(s)))->[x:=y(t);]p(x)");
allL('L=="\forall t (t>=0->[x:=y(t);](q(x)->p(x)))");
implyL('L=="t>=0->[x:=y(t);](q(x)->p(x))"); <(
  "t>=0":
    id,
  "[x:=y(t);](q(x)->p(x))":
    assignb('L=="[x:=y(t);](q(x)->p(x))");
    implyR('R=="\forall s (0<=s&s<=t->q(y(s)))->[x:=y(t);]p(x)");
    assignb('R=="[x:=y(t);]p(x)");
    implyL('L=="q(y(t))->p(y(t))"); <(
      "q(y(t))":
        allL("t", 'L=="\forall s (0<=s&s<=t->q(y(s)))");
        implyL('L=="0<=t&t<=t->q(y(t))"); <(
          "0<=t&t<=t":
            andR('R=="0<=t&t<=t"); <(
              "0<=t":
                QE,
              "t<=t":
                QE
            ),
          "q(y(t))":
            id
        ),
      "p(y(t))":
        id
    )
)
End.

End.
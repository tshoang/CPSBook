Theorem "CPSBook/Ex04.10/14 (Valid)"
Description "Ex4.10 (14): [{x' = 1}] x >= 0 -> [{x' = 2}] x >= 0 is valid".
/*
 * Copyright (c) 2024 University of Southampton.
 * Licensed under MIT license.
 * 
 * @author Son Hoang -- Initial model
 */
ProgramVariables
    Real x;
End.

Problem
    [{x' = 1}] x >= 0 -> [{x' = 2}] x >= 0
End.

/*
 * Note the instantiation existsR
 */
Tactic "CPSBook/Ex04.10/14 (Valid): Interactive"
implyR('R=="[{x'=1}]x>=0->[{x'=2}]x>=0");
dIRule('R=="[{x'=2}]x>=0"); <(
  "dI Init":
    boxd('L=="[{x'=1}]x>=0");
    notL('L=="!<{x'=1}>(!x>=0)");
    solve('R=="<{x'=1}>(!x>=0)");
    cut("!x>=0"); <(
      "Use":
        notGreaterEqual('L=="!x>=0");
        existsR("-x/2", 'R=="\exists t_ (t_>=0&t_+x < 0)");
        andR('R=="-x/2>=0&-x/2+x < 0"); <(
          "-x/2>=0":
            QE,
          "-x/2+x < 0":
            QE
        ),
      "Show":
        notR('R=="!x>=0");
        id
    ),
  "dI Step":
    Dassignb('R=="[x':=2;]x'>=0");
    QE using "true :: 2>=0 :: nil"
)
End.

Tactic "CPSBook/Ex04.10/14 (Valid): Automatic"
auto
End.

End.

Theorem "CPSBook/Ex04.10/06 (Valid)"
Description "Ex4.10 (6): x > 0 -> [{x' = 2}](x > 0 & [{x' = 1}]x > 0 & <{x' = - 2}> x = 0) is valid".
/*
 * Copyright (c) 2024 University of Southampton.
 * Licensed under MIT license.
 * 
 * @author Son Hoang -- Initial model
 */
ProgramVariables
  Real x;
End.

Problem
    x > 0 -> [{x' = 2}](x > 0 & [{x' = 1}]x > 0 & <{x' = - 2}> x = 0)
End.

/*
 * The proof tries to avoid solving the differential equation until the last step.
 */
Tactic "CPSBook/Ex04.10/05 (Valid): Interactive"
implyR('R=="x>0->[{x'=2}](x>0&[{x'=1}]x>0&<{x'=-2}>x=0)");
boxAnd('R=="[{x'=2}](x>0&[{x'=1}]x>0&<{x'=-2}>x=0)");
andR('R=="[{x'=2}]x>0&[{x'=2}]([{x'=1}]x>0&<{x'=-2}>x=0)"); <(
  "[{x'=2}]x>0":
    dIRule('R=="[{x'=2}]x>0"); <(
      "dI Init":
        id,
      "dI Step":
        Dassignb('R=="[x':=2;]x'>=0");
        QE
    ),
  "[{x'=2}]([{x'=1}]x>0&<{x'=-2}>x=0)":
    boxAnd('R=="[{x'=2}]([{x'=1}]x>0&<{x'=-2}>x=0)");
    andR('R=="[{x'=2}][{x'=1}]x>0&[{x'=2}]<{x'=-2}>x=0"); <(
      "[{x'=2}][{x'=1}]x>0":
        MR("x>0", 'R=="[{x'=2}][{x'=1}]x>0"); <(
          "Use Q->P":
            dIRule('R=="[{x'=2}]x>0"); <(
              "dI Init":
                id,
              "dI Step":
                Dassignb('R=="[x':=2;]x'>=0");
                QE
            ),
          "Show [a]Q":
            dIRule('R=="[{x'=1}]x>0"); <(
              "dI Init":
                id,
              "dI Step":
                Dassignb('R=="[x':=1;]x'>=0");
                QE
            )
        ),
      "[{x'=2}]<{x'=-2}>x=0":
        MR("x>0", 'R=="[{x'=2}]<{x'=-2}>x=0"); <(
          "Use Q->P":
            dIRule('R=="[{x'=2}]x>0"); <(
              "dI Init":
                id,
              "dI Step":
                Dassignb('R=="[x':=2;]x'>=0");
                QE
            ),
          "Show [a]Q":
            solve('R=="<{x'=-2}>x=0");
            QE
        )
    )
)
End.

Tactic "CPSBook/Ex04.10/05 (Valid): Automatic"
auto
End.
End.

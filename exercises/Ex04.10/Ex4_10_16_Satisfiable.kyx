Theorem "CPSBook/Ex04.10/16 (Satisfiable)"
Description "Ex4.10 (15): <{x' = 2}> x >= 0 -> [{x' = 1}] x >= 0 is satisfiable".
/*
 * Copyright (c) 2024 University of Southampton.
 * Licensed under MIT license.
 * 
 * @author Son Hoang -- Initial model
 */
ProgramVariables
    Real x;
End.

Problem
    x >= 0 <->
    (<{x' = 2}> x >= 0 -> [{x' = 1}] x >= 0)
End.

/*
 * Note the instantiation existsR and cut
 */
Tactic "CPSBook/Ex04.10/16 (Valid): Interactive"
equivR('R=="x>=0<-><{x'=2}>x>=0->[{x'=1}]x>=0"); <(
  "x>=0&(<{x'=2}>x>=0->[{x'=1}]x>=0)":
    implyR('R=="<{x'=2}>x>=0->[{x'=1}]x>=0");
    dIRule('R=="[{x'=1}]x>=0"); <(
      "dI Init":
        id,
      "dI Step":
        Dassignb('R=="[x':=1;]x'>=0");
        QE using "x>=0 :: true :: 1>=0 :: nil"
    ),
  "!x>=0&!(<{x'=2}>x>=0->[{x'=1}]x>=0)":
    implyL('L=="<{x'=2}>x>=0->[{x'=1}]x>=0"); <(
      "<{x'=2}>x>=0":
        solve('R=="<{x'=2}>x>=0");
        cut("!x>=0"); <(
          "Use":
            existsR("-x/2", 'R=="\exists t_ (t_>=0&2*t_+x>=0)");
            andR('R=="-x/2>=0&2*(-x/2)+x>=0"); <(
              "-x/2>=0":
                QE,
              "2*(-x/2)+x>=0":
                QE
            ),
          "Show":
            notR('R=="!x>=0");
            id
        ),
      "[{x'=1}]x>=0":
        boxd('L=="[{x'=1}]x>=0");
        notL('L=="!<{x'=1}>(!x>=0)");
        solve('R=="<{x'=1}>(!x>=0)");
        cut("!x>=0"); <(
          "Use":
            existsR("0", 'R=="\exists t_ (t_>=0&t_+x < 0)");
            andR('R=="0>=0&0+x < 0"); <(
              "0>=0":
                QE,
              "0+x < 0":
                QE
            ),
          "Show":
            notR('R=="!x>=0");
            id
        )
    )
)
End.

Tactic "CPSBook/Ex04.10/16 (Valid): Automatic"
auto
End.

End.

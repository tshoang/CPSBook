Theorem "CPSBook/exercises/Ex04.08/18 (Satisfiable)"
Description "Ex4.8 (18): (x > 0) -> [{x'=y}] x > 0 is satisfiable".
/*
 * Copyright (c) 2024 University of Southampton.
 * Licensed under MIT license.
 * 
 * @author Son Hoang -- Initial model
 */

ProgramVariables /* program variables may change their value over time */
  Real x, y;
End.

Problem
  (y >= 0 | x <= 0) /* Satisfiable exactly when y non-negative or x is non-positive initially*/
  <->
  (
      (x > 0)
    ->
      [{x'=y}] x > 0
  )
End.

Tactic "CPSBook/Ex04.08/18 (Satisfiable): Interactive"
equivR('R=="y>=0|x<=0<->x>0->[{x'=y}]x>0"); <(
  "(y>=0|x<=0)&(x>0->[{x'=y}]x>0)":
    implyR('R=="x>0->[{x'=y}]x>0");
    orL('L=="y>=0|x<=0"); <(
      "y>=0":
        ODE('R=="[{x'=y}]x>0"),
      "x<=0":
        smartQE
    ),
  "!(y>=0|x<=0)&!(x>0->[{x'=y}]x>0)":
    orR('R=="y>=0|x<=0");
    implyL('L=="x>0->[{x'=y}]x>0"); <(
      "x>0":
        smartQE,
      "[{x'=y}]x>0":
        solve('L=="[{x'=y}]x>0");
        smartQE
    )
)
End.

Tactic "CPSBook/Ex04.08/18 (Satisfiable): Automatic"
auto
End.
End.

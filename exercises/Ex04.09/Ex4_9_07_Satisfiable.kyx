Theorem "CPSBook/Ex04.09/07 (Satisfiable)"
Description "Ex4.9 (7): [{x := x*x + 1; {x' = 1}}*] x > 0 is satisfiable".
/*
 * Copyright (c) 2024 University of Southampton.
 * Licensed under MIT license.
 * 
 * @author Son Hoang -- Initial model
 */
ProgramVariables
  Real x;
End.

Problem
    x > 0 <-> [{x := x*x + 1; {x' = 1}}*] x > 0
End.

Tactic "CPSBook/Ex04.09/07 (Satisfiable): Interactive"
equivR('R=="x>0<->[{x:=x*x+1;{x'=1}}*]x>0"); <(
  "x>0&[{x:=x*x+1;{x'=1}}*]x>0":
    loop("x>0", 'R=="[{x:=x*x+1;{x'=1}}*]x>0"); <(
      "Init":
        id,
      "Post":
        id,
      "Step":
        composeb('R=="[x:=x*x+1;{x'=1}]x>0");
        assignb('R=="[x:=x*x+1;][{x'=1}]x>0");
        dIRule('R=="[{x'=1}]x>0"); <(
          "dI Init":
            QE,
          "dI Step":
            Dassignb('R=="[x':=1;]x'>=0");
            QE
        )
    ),
  "!x>0&![{x:=x*x+1;{x'=1}}*]x>0":
    iterateb('L=="[{x:=x*x+1;{x'=1}}*]x>0");
    andL('L=="x>0&[x:=x*x+1;{x'=1}][{x:=x*x+1;{x'=1}}*]x>0");
    id
)
End.

Tactic "CPSBook/Ex04.09/07 (Satisfiable): Automatic"
auto
End.

End.
